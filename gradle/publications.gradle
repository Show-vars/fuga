apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    ossrhReleasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
    ossrhSnapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = name
                url = 'http://github.com/bunjlabs/fuga'

                packaging 'jar'
                description 'Fuga Framework'

                scm {
                    connection = 'scm:git:git://github.com/bunjlabs/fuga.git'
                    developerConnection = 'scm:git:ssh://github.com/bunjlabs/fuga.git'
                    url = 'http://github.com/bunjlabs/fuga'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'show.vars'
                        name = 'Artem Shurygin'
                        email = 'artem.shurygin@bunjlabs.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def ossrhUsername = findProperty("ossrhUsername")
            def ossrhPassword = findProperty("ossrhPassword")

            url = version.endsWith("SNAPSHOT") ? ossrhSnapshotsRepoUrl : ossrhReleasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
