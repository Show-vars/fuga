apply plugin: "maven-publish"
apply plugin: "signing"
apply from: "$rootDir/gradle/publications.gradle"

jar {
    manifest.attributes["Implementation-Title"] = project.name
    manifest.attributes["Implementation-Version"] = project.version
    manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.header = project.name
    options.author = true
    options.encoding = "UTF-8"
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.use = true
    options.addStringOption('Xdoclint:none', '-quiet')

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'show.vars'
                        name = 'Artem Shurygin'
                        email = 'artem.shurygin@bunjlabs.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/bunjlabs/fuga.git'
                    developerConnection = 'scm:git:ssh://github.com/bunjlabs/fuga.git'
                    url = 'http://github.com/bunjlabs/fuga/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}